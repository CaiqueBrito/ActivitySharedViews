apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def artifactId = 'sharedlib'
def groupId = 'com.example.activitysharedviews'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Support
    implementation "com.android.support:appcompat-v7:$rootProject.ext.support_version"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //Junit
    testImplementation "junit:junit:$rootProject.ext.junit_version"
    //Espresso
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.espresso_runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso_core_version"
    //Mockito
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockito_core_version"
}

publishing {
    publications {
        library(MavenPublication) {
            setGroupId(groupId)
            setArtifactId(artifactId)
            version android.defaultConfig.versionName
            artifact(bundleReleaseAar)
        }
    }
}

task publishToM2() {
    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'publishToMavenLocal'
    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('publishToMavenLocal').mustRunAfter 'build'
}